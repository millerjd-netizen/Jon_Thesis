/*
 * ================================================================
 * ANAEROBIC DIGESTER CONTROL SYSTEM
 * Arduino GIGA R1
 * ================================================================
 * Tank 1 = BASELINE GROUP (Fixed ratios, no control)
 * Tank 2 = EXPERIMENTAL GROUP (Adaptive control)
 * ================================================================
 * DS18B20 Temperature Sensors
 * --------------------------
 * Tank 1 Temp -> D10 + 4.7k pullup to 5V
 * Tank 2 Temp -> D11 + 4.7k pullup to 5V
 * ================================================================
 * Outputs CSV lines prefixed with "DATA," for computer logging
 * Run digester_logger.py on computer to save hourly CSV files
 * ================================================================
 */

#include <math.h>
#include <OneWire.h>
#include <DallasTemperature.h>

// ================================================================
// PIN DEFINITIONS
// ================================================================

// ------------ Tank 1 (BASELINE) Sensor Pins ------------
const int PH_PIN_1   = A0;
const int ORP_PIN_1  = A1;
const int EC_PIN_1   = A2;
const int MFC_PIN_1  = A3;

// ------------ Tank 2 (EXPERIMENTAL) Sensor Pins ------------
const int EC_PIN_2   = A4;
const int PH_PIN_2   = A5;
const int ORP_PIN_2  = A6;
#define MFC_PIN_2 A9  // Pure analog pin

// ------------ DS18B20 Temperature Pins (Digital) ------------
#define TEMP_PIN_1 10
#define TEMP_PIN_2 11

// ------------ OneWire Buses ------------
OneWire oneWire1(TEMP_PIN_1);
DallasTemperature tempBus1(&oneWire1);
OneWire oneWire2(TEMP_PIN_2);
DallasTemperature tempBus2(&oneWire2);

// ------------ Pump Relay Pins (Active-High) ------------
const int PUMP_URINE_1    = 2;
const int PUMP_SPIRFEOX_1 = 3;
const int PUMP_MOLWINE_1  = 4;
const int PUMP_URINE_2    = 5;
const int PUMP_SPIRFEOX_2 = 6;
const int PUMP_MOLWINE_2  = 7;

// ================================================================
// SENSOR CALIBRATION CONSTANTS
// ================================================================
const float VREF = 3.3f;
const int   ADC_MAX = 4095;

// Base linear pH conversion
const float PH_SLOPE     = -5.59047f;
const float PH_INTERCEPT = 15.835f;

// Base linear EC conversion
const float EC_SLOPE_MSPERMV = 0.00864f;
const float EC_OFFSET_MS     = 0.033f;

// ORP base offset and oversampling
const float ORP_OFFSET_BASE = -1524.0f;
const int   ORP_OVERSAMPLES = 50;

// ================================================================
// PER-TANK SENSOR CALIBRATION
// ================================================================

// ORP trim (calibrated to 470mV solution)
// Tank1 read 467 -> needs +3
// Tank2 read 456 -> needs +14
const float ORP_TRIM_1_mV = +3.0f;
const float ORP_TRIM_2_mV = +14.0f;

// pH quadratic correction: pH_true = A*pH_meas^2 + B*pH_meas + C
// Tank 1: measured 10.00, 7.04, 4.12 for buffers 10, 7, 5
const float PH1_A = 0.05588129365f;
const float PH1_B = 0.06129626971f;
const float PH1_C = 3.79890793785f;

// Tank 2: measured 10.25, 7.31, 4.42 for buffers 10, 7, 5
const float PH2_A = -0.07490f;
const float PH2_B = 1.98897f;
const float PH2_C = -5.76500f;

// EC linear correction: EC_true = A*EC_meas + C
// Tank1: 4.37 -> 1.413, 18.03 -> 12.88
const float EC1_A = 0.83945827233f;
const float EC1_C = -2.25543265007f;

// Tank2: 3.86 -> 1.413, 17.06 -> 12.88
const float EC2_A = 0.86871212121f;
const float EC2_C = -1.94022878788f;

// ================================================================
// PUMP CALIBRATION (mL per second)
// ================================================================
// Calculated from 2-second tests:
// Tank 1 Urine:    avg(1.34, 1.27, 1.37) / 2 = 0.663 mL/s
// Tank 2 Urine:    avg(1.19, 1.22) / 2 = 0.603 mL/s
// Tank 1 SpirFeOx: avg(0.81, 0.78, 0.78) / 2 = 0.395 mL/s
// Tank 2 SpirFeOx: avg(0.69, 0.79, 0.69) / 2 = 0.362 mL/s
// Tank 1 MolWine:  avg(1.43, 1.49) / 2 = 0.730 mL/s
// Tank 2 MolWine:  avg(0.87, 1.22, 1.04) / 2 = 0.522 mL/s

const float ML_PER_SEC_URINE_1    = 0.663f;
const float ML_PER_SEC_SPIRFEOX_1 = 0.395f;
const float ML_PER_SEC_MOLWINE_1  = 0.730f;
const float ML_PER_SEC_URINE_2    = 0.603f;
const float ML_PER_SEC_SPIRFEOX_2 = 0.362f;
const float ML_PER_SEC_MOLWINE_2  = 0.522f;

// ================================================================
// REACTOR CONFIGURATION
// ================================================================
const float REACTOR_VOLUME_L = 0.875f;  // 875 mL
const float BASE_HRT = 24.0f;
const float EMERGENCY_HRT = 3.0f;
const unsigned long DOSE_INTERVAL_MS = 300000;  // 5 minutes

// ================================================================
// TARGET RANGES
// ================================================================
const float PH_MIN = 6.5f;
const float PH_MAX = 7.5f;
const float PH_TARGET = 7.0f;

// ================================================================
// BASELINE GROUP - FIXED RATIOS
// ================================================================
const float BASELINE_FRAC_URINE = 0.11f;
const float BASELINE_FRAC_SPIRFEOX = 0.32f;
const float BASELINE_FRAC_MOLWINE = 0.57f;

// ================================================================
// DATA STRUCTURES
// ================================================================
struct SensorData {
  float pH;
  float EC_mS;
  float ORP_mV;
  float MFC_mV;
  float Temp_C;
};

struct DoseVolumes {
  float Q_urine_mL;
  float Q_spirfeox_mL;
  float Q_molwine_mL;
  float Q_total_mL;
  float HRT;
};

// ================================================================
// GLOBAL STATE
// ================================================================
unsigned long lastDoseTime = 0;
unsigned long doseCount = 0;
float elapsedHours = 0.0f;

// ================================================================
// HELPER FUNCTIONS
// ================================================================

float rawTo_mV(int raw) {
  return raw * 3300.0f / 4095.0f;
}

float readDS18B20(DallasTemperature& bus) {
  bus.requestTemperatures();
  float c = bus.getTempCByIndex(0);
  if (c == DEVICE_DISCONNECTED_C || c == 85.0f) return NAN;
  if (c < -55.0f || c > 125.0f) return NAN;
  return c;
}

// ================================================================
// SENSOR READING FUNCTIONS
// ================================================================

float readORP(int pin, float trim_mV) {
  long sum = 0;
  for (int i = 0; i < ORP_OVERSAMPLES; i++) {
    sum += analogRead(pin);
    delayMicroseconds(100);
  }
  float avg = sum / (float)ORP_OVERSAMPLES;
  float mV = (avg * VREF / ADC_MAX) * 1000.0f + ORP_OFFSET_BASE;
  return mV + trim_mV;
}

float readPH_corrected(int pin, float A, float B, float C) {
  float v = analogRead(pin) * (VREF / ADC_MAX);
  float pH_meas = PH_SLOPE * v + PH_INTERCEPT;
  return (A * pH_meas * pH_meas) + (B * pH_meas) + C;
}

float readEC_corrected(int pin, float A, float C) {
  float ec_meas = EC_SLOPE_MSPERMV * rawTo_mV(analogRead(pin)) + EC_OFFSET_MS;
  return A * ec_meas + C;
}

float readMFC(int pin) {
  return rawTo_mV(analogRead(pin));
}

SensorData readTank1Sensors() {
  SensorData data;
  data.pH     = readPH_corrected(PH_PIN_1, PH1_A, PH1_B, PH1_C);
  data.EC_mS  = readEC_corrected(EC_PIN_1, EC1_A, EC1_C);
  data.ORP_mV = readORP(ORP_PIN_1, ORP_TRIM_1_mV);
  data.MFC_mV = readMFC(MFC_PIN_1);
  data.Temp_C = readDS18B20(tempBus1);
  return data;
}

SensorData readTank2Sensors() {
  SensorData data;
  data.pH     = readPH_corrected(PH_PIN_2, PH2_A, PH2_B, PH2_C);
  data.EC_mS  = readEC_corrected(EC_PIN_2, EC2_A, EC2_C);
  data.ORP_mV = readORP(ORP_PIN_2, ORP_TRIM_2_mV);
  data.MFC_mV = rawTo_mV(analogRead(MFC_PIN_2));
  data.Temp_C = readDS18B20(tempBus2);
  return data;
}

// ================================================================
// CONTROL FUNCTIONS
// ================================================================

DoseVolumes calculateBaselineDose() {
  DoseVolumes dose;
  dose.HRT = BASE_HRT;
  dose.Q_total_mL = (REACTOR_VOLUME_L * 1000.0f) / (dose.HRT * 60.0f) * 5.0f;  // 5 min interval
  dose.Q_urine_mL   = dose.Q_total_mL * BASELINE_FRAC_URINE;
  dose.Q_spirfeox_mL = dose.Q_total_mL * BASELINE_FRAC_SPIRFEOX;
  dose.Q_molwine_mL = dose.Q_total_mL * BASELINE_FRAC_MOLWINE;
  return dose;
}

DoseVolumes calculateExperimentalDose(const SensorData& sensors) {
  DoseVolumes dose;
  
  if (sensors.pH < PH_MIN || sensors.pH > PH_MAX) {
    dose.HRT = EMERGENCY_HRT;
  } else {
    dose.HRT = BASE_HRT;
  }
  
  dose.Q_total_mL = (REACTOR_VOLUME_L * 1000.0f) / (dose.HRT * 60.0f) * 5.0f;  // 5 min interval
  
  float f_urine = 0.15f;
  float f_spirfeox = 0.30f;
  float f_molwine = 0.55f;
  
  float pH_error = sensors.pH - PH_TARGET;
  
  if (fabs(pH_error) > 0.3f) {
    if (pH_error > 0) {
      f_molwine = min(0.8f, f_molwine + 0.3f);
      f_urine = max(0.05f, f_urine - 0.2f);
    } else {
      f_urine = min(0.8f, f_urine + 0.3f);
      f_molwine = max(0.05f, f_molwine - 0.2f);
    }
  }
  
  float total_frac = f_urine + f_spirfeox + f_molwine;
  f_urine /= total_frac;
  f_spirfeox /= total_frac;
  f_molwine /= total_frac;
  
  dose.Q_urine_mL = dose.Q_total_mL * f_urine;
  dose.Q_spirfeox_mL = dose.Q_total_mL * f_spirfeox;
  dose.Q_molwine_mL = dose.Q_total_mL * f_molwine;
  
  return dose;
}

// ================================================================
// PUMP CONTROL
// ================================================================

void allPumpsOff() {
  digitalWrite(PUMP_URINE_1, LOW);
  digitalWrite(PUMP_SPIRFEOX_1, LOW);
  digitalWrite(PUMP_MOLWINE_1, LOW);
  digitalWrite(PUMP_URINE_2, LOW);
  digitalWrite(PUMP_SPIRFEOX_2, LOW);
  digitalWrite(PUMP_MOLWINE_2, LOW);
}

void runPump(int pin, float mL, float mL_per_sec, const char* pumpName, int tank) {
  if (mL <= 0) return;
  unsigned long run_time_ms = (unsigned long)((mL / mL_per_sec) * 1000.0f);
  
  Serial.print("  DOSING T");
  Serial.print(tank);
  Serial.print(" ");
  Serial.print(pumpName);
  Serial.print(": ");
  Serial.print(mL, 3);
  Serial.print(" mL (");
  Serial.print(run_time_ms);
  Serial.println(" ms)");
  
  digitalWrite(pin, HIGH);
  delay(run_time_ms);
  digitalWrite(pin, LOW);
  delay(100);
}

void executeDose_Tank1(const DoseVolumes& dose) {
  Serial.println("--- TANK 1 DOSING ---");
  runPump(PUMP_URINE_1, dose.Q_urine_mL, ML_PER_SEC_URINE_1, "URINE", 1);
  runPump(PUMP_SPIRFEOX_1, dose.Q_spirfeox_mL, ML_PER_SEC_SPIRFEOX_1, "SPIRFEOX", 1);
  runPump(PUMP_MOLWINE_1, dose.Q_molwine_mL, ML_PER_SEC_MOLWINE_1, "MOLWINE", 1);
  Serial.println("--- TANK 1 DONE ---");
}

void executeDose_Tank2(const DoseVolumes& dose) {
  Serial.println("--- TANK 2 DOSING ---");
  runPump(PUMP_URINE_2, dose.Q_urine_mL, ML_PER_SEC_URINE_2, "URINE", 2);
  runPump(PUMP_SPIRFEOX_2, dose.Q_spirfeox_mL, ML_PER_SEC_SPIRFEOX_2, "SPIRFEOX", 2);
  runPump(PUMP_MOLWINE_2, dose.Q_molwine_mL, ML_PER_SEC_MOLWINE_2, "MOLWINE", 2);
  Serial.println("--- TANK 2 DONE ---");
}

// ================================================================
// OUTPUT FUNCTIONS
// ================================================================

const char* getPhase_Baseline(float pH) {
  if (pH < PH_MIN) return "pH_LOW";
  if (pH > PH_MAX) return "pH_HIGH";
  return "normal";
}

const char* getPhase_Experimental(float pH) {
  if (pH < PH_MIN) return "pH_LOW_CORR";
  if (pH > PH_MAX) return "pH_HIGH_CORR";
  return "normal";
}

void printHeader() {
  Serial.println(F("================================================================================================================================================"));
  Serial.println(F("ANAEROBIC DIGESTER DUAL-TANK CONTROL SYSTEM"));
  Serial.println(F("Tank 1 = BASELINE (Fixed Ratios) | Tank 2 = EXPERIMENTAL (Adaptive Control)"));
  Serial.println(F("Reactor Volume: 875 mL | Dose Interval: 5 minutes"));
  Serial.println(F("================================================================================================================================================"));
}

void printTankData(float time_hr, int tank, const char* group, const char* phase, 
                   const DoseVolumes& dose, const SensorData& sensors) {
  
  Serial.println(F(""));
  Serial.print(F("=== TANK "));
  Serial.print(tank);
  Serial.print(F(" ("));
  Serial.print(group);
  Serial.print(F(") === Time: "));
  Serial.print(time_hr, 2);
  Serial.print(F(" hr | Phase: "));
  Serial.println(phase);
  
  Serial.println(F("SENSORS:"));
  Serial.print(F("  pH="));
  Serial.print(sensors.pH, 2);
  Serial.print(F("  EC="));
  Serial.print(sensors.EC_mS, 3);
  Serial.print(F(" mS/cm  ORP="));
  Serial.print(sensors.ORP_mV, 1);
  Serial.print(F(" mV  MFC="));
  Serial.print(sensors.MFC_mV, 1);
  Serial.print(F(" mV  Temp="));
  if (isnan(sensors.Temp_C)) {
    Serial.println(F("NA"));
  } else {
    Serial.print(sensors.Temp_C, 2);
    Serial.println(F(" C"));
  }
  
  Serial.println(F("DOSE PLAN:"));
  Serial.print(F("  URINE="));
  Serial.print(dose.Q_urine_mL, 3);
  Serial.print(F(" mL  SPIRFEOX="));
  Serial.print(dose.Q_spirfeox_mL, 3);
  Serial.print(F(" mL  MOLWINE="));
  Serial.print(dose.Q_molwine_mL, 3);
  Serial.print(F(" mL  TOTAL="));
  Serial.print(dose.Q_total_mL, 3);
  Serial.print(F(" mL  HRT="));
  Serial.print(dose.HRT, 1);
  Serial.println(F(" hr"));
  
  // CSV output line for Python logger
  Serial.print(F("DATA,"));
  Serial.print(time_hr, 4); Serial.print(F(","));
  Serial.print(tank); Serial.print(F(","));
  Serial.print(group); Serial.print(F(","));
  Serial.print(phase); Serial.print(F(","));
  Serial.print(dose.Q_urine_mL, 4); Serial.print(F(","));
  Serial.print(dose.Q_spirfeox_mL, 4); Serial.print(F(","));
  Serial.print(dose.Q_molwine_mL, 4); Serial.print(F(","));
  Serial.print(dose.Q_total_mL, 4); Serial.print(F(","));
  Serial.print(sensors.pH, 3); Serial.print(F(","));
  Serial.print(sensors.EC_mS, 3); Serial.print(F(","));
  Serial.print(sensors.ORP_mV, 2); Serial.print(F(","));
  Serial.print(sensors.MFC_mV, 2); Serial.print(F(","));
  if (isnan(sensors.Temp_C)) {
    Serial.print(F("NA"));
  } else {
    Serial.print(sensors.Temp_C, 2);
  }
  Serial.print(F(","));
  Serial.println(dose.HRT, 1);
}

// ================================================================
// SETUP
// ================================================================

void setup() {
  Serial.begin(115200);
  while (!Serial);
  delay(1000);
  
  pinMode(PUMP_URINE_1, OUTPUT);
  pinMode(PUMP_SPIRFEOX_1, OUTPUT);
  pinMode(PUMP_MOLWINE_1, OUTPUT);
  pinMode(PUMP_URINE_2, OUTPUT);
  pinMode(PUMP_SPIRFEOX_2, OUTPUT);
  pinMode(PUMP_MOLWINE_2, OUTPUT);
  
  allPumpsOff();
  analogReadResolution(12);
  
  // Initialize DS18B20 temperature sensors
  tempBus1.begin();
  tempBus2.begin();
  tempBus1.setWaitForConversion(true);
  tempBus2.setWaitForConversion(true);
  tempBus1.setResolution(12);
  tempBus2.setResolution(12);
  
  Serial.println(F(""));
  Serial.println(F("================================================================"));
  Serial.println(F("  ANAEROBIC DIGESTER CONTROL SYSTEM - STARTING"));
  Serial.println(F("  Reactor Volume: 875 mL"));
  Serial.println(F("  Dose Interval: 5 minutes"));
  Serial.println(F("  Run digester_logger.py on computer for CSV logging"));
  Serial.println(F("================================================================"));
  Serial.println(F(""));
  
  Serial.print(F("DS18B20 on D10 (Tank 1): "));
  Serial.println(tempBus1.getDeviceCount());
  Serial.print(F("DS18B20 on D11 (Tank 2): "));
  Serial.println(tempBus2.getDeviceCount());
  Serial.println(F(""));
  
  Serial.println(F("PUMP CALIBRATION (mL/sec):"));
  Serial.print(F("  T1 Urine: ")); Serial.println(ML_PER_SEC_URINE_1, 3);
  Serial.print(F("  T1 SpirFeOx: ")); Serial.println(ML_PER_SEC_SPIRFEOX_1, 3);
  Serial.print(F("  T1 MolWine: ")); Serial.println(ML_PER_SEC_MOLWINE_1, 3);
  Serial.print(F("  T2 Urine: ")); Serial.println(ML_PER_SEC_URINE_2, 3);
  Serial.print(F("  T2 SpirFeOx: ")); Serial.println(ML_PER_SEC_SPIRFEOX_2, 3);
  Serial.print(F("  T2 MolWine: ")); Serial.println(ML_PER_SEC_MOLWINE_2, 3);
  Serial.println(F(""));
  
  Serial.println(F("CSVHEADER,Time_hr,Tank,Group,Phase,Q_urine_mL,Q_spirfeox_mL,Q_molwine_mL,Q_total_mL,pH,EC_mS,ORP_mV,MFC_mV,Temp_C,HRT_hr"));
  
  printHeader();
  lastDoseTime = millis();
}

// ================================================================
// MAIN LOOP
// ================================================================

void loop() {
  unsigned long currentTime = millis();
  
  if (currentTime - lastDoseTime >= DOSE_INTERVAL_MS) {
    elapsedHours = (doseCount * DOSE_INTERVAL_MS) / 3600000.0f;
    
    SensorData sensors1 = readTank1Sensors();
    DoseVolumes dose1 = calculateBaselineDose();
    const char* phase1 = getPhase_Baseline(sensors1.pH);
    
    printTankData(elapsedHours, 1, "BASELINE", phase1, dose1, sensors1);
    executeDose_Tank1(dose1);
    
    SensorData sensors2 = readTank2Sensors();
    DoseVolumes dose2 = calculateExperimentalDose(sensors2);
    const char* phase2 = getPhase_Experimental(sensors2.pH);
    
    printTankData(elapsedHours, 2, "EXPERIMENTAL", phase2, dose2, sensors2);
    executeDose_Tank2(dose2);
    
    doseCount++;
    Serial.println(F("================================================================================"));
    
    lastDoseTime = currentTime;
  }
}
